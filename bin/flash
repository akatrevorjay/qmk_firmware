#!/bin/zsh
set -eo pipefail
# set -xv

self=${0:t}
root=${0:h}/..

make() {
	local cmd=(
		make -C $root
	)

	! (( ${+commands[bear]} )) || cmd=(bear $cmd)

	command $cmd -j$(nproc) "$@" || command $cmd -j1 "$@"
}

flash() {
	usage() {
		echo "Usage: $0 KEYBOARD_NAME"
	}

	if [[ $# -eq 0 ]]; then
		usage >&2
		return 1
	fi

	local target=$1; shift
	case $target in
		*:*) ;;

		ergodox_infinity)
			if [[ $# -eq 0 ]]; then
				local side
				for side (right left); do
					echo
					echo "  >> connect $side keyboard and press enter to flash <<"
					read

					flash $target MASTER=$side "$@"
				done

				return
			fi

			export SERIAL=mk20dx256vlh7
			target="ergodox_infinity:trevorj:dfu-util"
			;;

		planck) target="planck/light" ;&
		*)
			target=${target}:trevorj:dfu ;;
	esac

	set -xv
	make $target "$@"
	set +xv
}

main() {
	case $self in
		flash-*)	set -- ${self#flash-} "$@" ;;
	esac

	flash "$@"
}

main "$@"


